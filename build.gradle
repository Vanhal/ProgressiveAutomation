
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

def ENV = System.getenv()

ext.configFile = file "build.properties"

configFile.withReader {
    // read config. it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

def y = config.droneAlter as Integer
def x = y

if(ENV.DRONE_BUILD_NUMBER)
x = ENV.DRONE_BUILD_NUMBER as Integer

config.buildNum = x - y

version = config.mod_version + "." + config.buildNum
group= "com.vanhal.progressiveautomation" 
archivesBaseName = "ProgressiveAutomation-" + config.minecraft_version

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"
    
    replace "@VERSION@", project.version
    mappings = "snapshot_20160312"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
